/PROG  A_1PA005
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "1/2 Area plate";
PROG_SIZE	= 13354;
CREATE		= DATE 19-04-01  TIME 18:06:44;
MODIFIED	= DATE 19-04-03  TIME 22:49:32;
FILE_NAME	= A_1PA005;
VERSION		= 0;
LINE_COUNT	= 681;
MEMORY_SIZE	= 14298;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  !2019-04-01 ;
   2:  !FANUC M20iA ;
   3:   ;
   4:  !MET NIEUWE CENTREERPLAAT EN ;
   5:  !PRINTER ;
   6:   ;
   7:  UFRAME_NUM=1 ;
   8:  UTOOL_NUM=1 ;
   9:   ;
  10:  IF R[90:Program gestart]=0,JMP LBL[1000] ;
  11:   ;
  12:  !START PROGRAMMA AFLOOP ;
  13:   ;
  14:  OVERRIDE=100% ;
  15:  CALL TEKST(3) ;
  16:  UFRAME_NUM=1 ;
  17:  UTOOL_NUM=1 ;
  18:J P[1:rust positie] 20% CNT100    ;
  19:   ;
  20:  !Grijpers open ;
  21:  Open hand 1    ;
  22:  Open hand 2    ;
  23:   ;
  24:  LBL[10:NAAR INDUIK] ;
  25:  IF DI[7:Automaat]=OFF,JMP LBL[530] ;
  26:  IF DI[6:Matrijs dicht]=ON,JMP LBL[15] ;
  27:  JMP LBL[10] ;
  28:   ;
  29:  LBL[15] ;
  30:  !Uitstoter terug ;
  31:  DO[11017]=OFF ;
  32:   ;
  33:J P[3:naar SGM] 50% CNT100    ;
  34:J P[6:induik] 50% CNT100    ;
  35:   ;
  36:  !GRIJPERS OPEN ;
  37:  Open hand 1    ;
  38:  Open hand 2    ;
  39:   ;
  40:  LBL[20:Cyclusteller] ;
  41:  R[46:Cyclusteller]=R[46:Cyclusteller]+1    ;
  42:   ;
  43:  $WAITTMOUT=500 ;
  44:  !Controle induikvoorwaarde ;
  45:  WAIT DO[11161]=ON TIMEOUT,LBL[570] ;
  46:  R[1:Cyclustijd]=TIMER[1]    ;
  47:   ;
  48:  LBL[25] ;
  49:  !Controle grijpers open ;
  50:  IF RI[1:Bek B Open]=OFF OR RI[3:Bek A Open]=OFF,JMP LBL[570] ;
  51:   ;
  52:  !Controle induikvoorwaarde ;
  53:  IF DO[11161]=OFF,JMP LBL[570] ;
  54:   ;
  55:  !Wacht op matrijs open ;
  56:  IF DO[11033]=ON,JMP LBL[30] ;
  57:   ;
  58:  !Check machine in automaat ;
  59:  IF DI[7:Automaat]=OFF,JMP LBL[530] ;
  60:  JMP LBL[25] ;
  61:   ;
  62:  LBL[30:UITNAME] ;
  63:  IF R[46:Cyclusteller]<2,JMP LBL[31] ;
  64:   ;
  65:  !% Snelheid naar 100% ;
  66:  OVERRIDE=100% ;
  67:   ;
  68:  LBL[31:Pak producten] ;
  69:  TIMER[1]=RESET ;
  70:  !UITNEMEN PRODUCT ;
  71:L P[9:voor pak] 1500mm/sec CNT10    ;
  72:L P[10:pak pos] 500mm/sec FINE    ;
  73:   ;
  74:  !Uitstoter naar voren ;
  75:  DO[11017]=ON ;
  76:   ;
  77:  $WAITTMOUT=500 ;
  78:  WAIT DI[3:Uitw. terug]=OFF TIMEOUT,LBL[33] ;
  79:   ;
  80:  !Vertraging pakken ;
  81:  WAIT    .40(sec) ;
  82:  Close hand 1    ;
  83:  Close hand 2    ;
  84:  WAIT R[5] ;
  85:   ;
  86:  LBL[33:Na pak] ;
  87:L P[11:Na pak] 250mm/sec CNT30    ;
  88:  !Uitstoter terug ;
  89:  DO[11017]=OFF ;
  90:L P[15:buiten SGM] 1500mm/sec CNT100    ;
  91:   ;
  92:  !% Snelheid = register 49 ;
  93:  OVERRIDE=R[49:Snelheid] ;
  94:   ;
  95:  LBL[40:UITNAME CONTROLE] ;
  96:  IF RI[1:Bek B Open]=OFF AND RI[2:Bek B product aanwezig]=ON AND RI[3:Bek A Open]=OFF AND RI[4:Bek A product aanwezig]=ON,JMP LBL[50] ;
  97:  IF R[100:Product controle]>1,JMP LBL[500] ;
  98:  WAIT    .01(sec) ;
  99:  R[100:Product controle]=R[100:Product controle]+.01    ;
 100:  JMP LBL[40] ;
 101:   ;
 102:  LBL[50:UITNAME OK ] ;
 103:  R[100:Product controle]=0    ;
 104:   ;
 105:  !Spuitgietmachine sluiten ;
 106:  DO[11025]=PULSE,0.5sec ;
 107:  R[2:Induiktijd]=TIMER[1]    ;
 108:  IF DI[10:Fout product]=ON,JMP LBL[510] ;
 109:J P[50:deurpos] 85% CNT80    ;
 110:   ;
 111:  LBL[60:OPSTART AFKEUR] ;
 112:  IF R[10:Opstart aantal]=0,JMP LBL[200] ;
 113:  IF R[29:Start afleg]=0,JMP LBL[510] ;
 114:   ;
 115:  !KEREN PAARS ;
 116:  LBL[200:Keren] ;
 117:  CALL B_KEREN2    ;
 118:   ;
 119:  !Controle fouten na keren ;
 120:  IF R[81:Kerunit niet vry]=1,JMP LBL[505] ;
 121:  IF R[82:Grijper open]=1,JMP LBL[775] ;
 122:  IF R[83:Grijpcontr keren]=1,JMP LBL[770] ;
 123:  IF R[84:Lucht keerunit]=1,JMP LBL[778] ;
 124:   ;
 125:  !PRINTEN BERGSTEIN ;
 126:  LBL[210:Printen] ;
 127:   ;
 128:  !Controle lekbak/magneet aanwezig ;
 129:  IF DI[72:Afdekkap aanwzg]=OFF,JMP LBL[211] ;
 130:  CALL TEKST(800) ;
 131:   ;
 132:  !Robot veilig bewegen van printer ;
 133:  WAIT DI[72:Afdekkap aanwzg]=OFF    ;
 134:  CALL TEKST(3) ;
 135:   ;
 136:  LBL[211:Start printen] ;
 137:  CALL PRINTEN2    ;
 138:  UFRAME_NUM=1 ;
 139:   ;
 140:  !% Snelheid = register 49 ;
 141:  OVERRIDE=R[49:Snelheid] ;
 142:   ;
 143:  !Controle fouten na printen ;
 144:  IF R[38:Hoogte fout]=1,JMP LBL[720] ;
 145:   ;
 146:  !AFLEGGEN OP CORONABAND ;
 147:  LBL[230:Afleggen] ;
 148:  CALL B_AFLEG2    ;
 149:   ;
 150:  UFRAME_NUM=1 ;
 151:   ;
 152:  !Controle fouten na afleggen ;
 153:  IF R[42:Grijpfout bufffe]=1,JMP LBL[730] ;
 154:  IF R[43:Bufferstapel vol]=1,JMP LBL[740] ;
 155:  IF R[39:Buffer niet vrij]=1,JMP LBL[750] ;
 156:   ;
 157:  !Naar begin programma ;
 158:  LBL[490] ;
 159:  UFRAME_NUM=1 ;
 160:J P[50:deurpos] 100% CNT60    ;
 161:  Open hand 1    ;
 162:  Open hand 2    ;
 163:J P[3:naar SGM] 85% CNT100    ;
 164:J P[6:induik] 85% CNT100    ;
 165:  JMP LBL[20] ;
 166:   ;
 167:   ;
 168:  !******STORINGEN****** ;
 169:   ;
 170:  LBL[500:UITNAME FOUT] ;
 171:  CALL TEKST(500) ;
 172:  R[100:Product controle]=0    ;
 173:J P[1:rust positie] 30% FINE    ;
 174:  CALL DUMPEN    ;
 175:  WAIT DI[7:Automaat]=OFF    ;
 176:   ;
 177:  !Lamp USER 2 AAN op fanuc kast ;
 178:  DO[54:User2]=ON ;
 179:  WAIT DI[45:USER 2]=ON    ;
 180:   ;
 181:  !Lamp USER 2 AAN op fanuc kast ;
 182:  DO[54:User2]=OFF ;
 183:  END ;
 184:   ;
 185:  LBL[505:GRIJPFOUT KEREN] ;
 186:  CALL TEKST(736) ;
 187:  R[100:Product controle]=0    ;
 188:  R[81:Kerunit niet vry]=0    ;
 189:J P[1:rust positie] 30% FINE    ;
 190:  CALL DUMPEN    ;
 191:  WAIT DI[7:Automaat]=OFF    ;
 192:   ;
 193:  !Lamp USER 2 AAN op fanuc kast ;
 194:  DO[54:User2]=ON ;
 195:  WAIT DI[45:USER 2]=ON    ;
 196:   ;
 197:  !Lamp USER 2 AAN op fanuc kast ;
 198:  DO[54:User2]=OFF ;
 199:  END ;
 200:   ;
 201:  LBL[506:Cntle Keren vry] ;
 202:  IF DI[71:Keerunit leeg]=OFF,JMP LBL[507] ;
 203:   ;
 204:  CALL TEKST(736) ;
 205:  !Lamp USER 2 AAN op fanuc kast ;
 206:  DO[54:User2]=ON ;
 207:  WAIT DI[45:USER 2]=ON    ;
 208:   ;
 209:  !Lamp USER 2 AAN op fanuc kast ;
 210:  DO[54:User2]=OFF ;
 211:  JMP LBL[506] ;
 212:   ;
 213:  LBL[507] ;
 214:  END ;
 215:   ;
 216:  LBL[510:DUMPEN PRODUCTEN] ;
 217:  CALL DUMPEN    ;
 218:  R[28:Act dump]=R[28:Act dump]+1    ;
 219:J P[3:naar SGM] 30% FINE    ;
 220:J P[6:induik] 30% FINE    ;
 221:  IF R[28:Act dump]>=R[10:Opstart aantal],JMP LBL[520] ;
 222:  JMP LBL[20] ;
 223:  LBL[520] ;
 224:  R[29:Start afleg]=1    ;
 225:  JMP LBL[20] ;
 226:   ;
 227:  LBL[530:SGM UIT AUTOMAAT] ;
 228:  R[100:Product controle]=0    ;
 229:J P[1:rust positie] 30% FINE    ;
 230:   ;
 231:  !DO :Blower corona uit ;
 232:  DO[51:IONblower Corona]=OFF ;
 233:  END ;
 234:   ;
 235:  LBL[570] ;
 236:J P[3:naar SGM] 65% CNT100    ;
 237:   ;
 238:  LBL[580:Gryprfout induik] ;
 239:  CALL TEKST(581) ;
 240:J P[1:rust positie] 30% FINE    ;
 241:  CALL DUMPEN    ;
 242:   ;
 243:  WAIT DI[7:Automaat]=OFF    ;
 244:  !Lamp USER 2 AAN op fanuc kast ;
 245:  DO[54:User2]=ON ;
 246:  WAIT DI[45:USER 2]=ON    ;
 247:   ;
 248:  !Lamp USER 2 AAN op fanuc kast ;
 249:  DO[54:User2]=OFF ;
 250:  END ;
 251:   ;
 252:  LBL[720:HOOGTE FOUT] ;
 253:  CALL TEKST(725) ;
 254:  R[100:Product controle]=0    ;
 255:  R[38:Hoogte fout]=0    ;
 256:J P[1:rust positie] 30% FINE    ;
 257:   ;
 258:  CALL DUMPEN    ;
 259:   ;
 260:  WAIT DI[7:Automaat]=OFF    ;
 261:   ;
 262:  !Lamp USER 2 AAN op fanuc kast ;
 263:  DO[54:User2]=ON ;
 264:  WAIT DI[45:USER 2]=ON    ;
 265:   ;
 266:  !Lamp USER 2 AAN op fanuc kast ;
 267:  DO[54:User2]=OFF ;
 268:  END ;
 269:   ;
 270:  LBL[730:GRIJPER FOUT BUF] ;
 271:  CALL TEKST(730) ;
 272:  R[100:Product controle]=0    ;
 273:  R[42:Grijpfout bufffe]=0    ;
 274:  R[9:Act R8]=0    ;
 275:J P[1:rust positie] 30% FINE    ;
 276:   ;
 277:  CALL DUMPEN    ;
 278:   ;
 279:  WAIT DI[7:Automaat]=OFF    ;
 280:   ;
 281:  !Lamp USER 2 AAN op fanuc kast ;
 282:  DO[54:User2]=ON ;
 283:  WAIT DI[45:USER 2]=ON    ;
 284:   ;
 285:  !Lamp USER 2 AAN op fanuc kast ;
 286:  DO[54:User2]=OFF ;
 287:   ;
 288:  LBL[731:Contrle buf leeg] ;
 289:  IF DI[22:Buffer vrij]=ON,JMP LBL[732] ;
 290:   ;
 291:  CALL TEKST(1200) ;
 292:  !Lamp USER 2 AAN op fanuc kast ;
 293:  DO[54:User2]=ON ;
 294:  WAIT DI[45:USER 2]=ON    ;
 295:   ;
 296:  !Lamp USER 2 AAN op fanuc kast ;
 297:  DO[54:User2]=OFF ;
 298:  JMP LBL[7311] ;
 299:   ;
 300:  LBL[732:Buffer is vrij] ;
 301:  !DO :RODE LAMP/BUZZER  UIT ;
 302:  DO[49:Buzzer hek]=OFF ;
 303:  DO[50:Rode lamp hek]=OFF ;
 304:  END ;
 305:   ;
 306:  LBL[735:Centrer niet vry] ;
 307:  CALL TEKST(735) ;
 308:  R[47:Centrer niet vri]=0    ;
 309:J P[1:rust positie] 30% FINE    ;
 310:  CALL DUMPEN    ;
 311:  WAIT DI[7:Automaat]=OFF    ;
 312:   ;
 313:  !Lamp USER 2 AAN op fanuc kast ;
 314:  DO[54:User2]=ON ;
 315:  WAIT DI[45:USER 2]=ON    ;
 316:   ;
 317:  !Lamp USER 2 AAN op fanuc kast ;
 318:  DO[54:User2]=OFF ;
 319:   ;
 320:  LBL[736:Cntle positi vry] ;
 321:  IF DI[23:Positioneer vrij]=OFF,JMP LBL[737] ;
 322:   ;
 323:  CALL TEKST(735) ;
 324:  !Lamp USER 2 AAN op fanuc kast ;
 325:  DO[54:User2]=ON ;
 326:  WAIT DI[45:USER 2]=ON    ;
 327:   ;
 328:  !Lamp USER 2 AAN op fanuc kast ;
 329:  DO[54:User2]=OFF ;
 330:  JMP LBL[736] ;
 331:   ;
 332:  LBL[737:Positioneer vry] ;
 333:  END ;
 334:   ;
 335:  LBL[738:GRYPFOUT MASKER] ;
 336:  CALL TEKST(738) ;
 337:  R[36:Grijpfout masker]=0    ;
 338:J P[1:rust positie] 30% FINE    ;
 339:   ;
 340:  WAIT DI[7:Automaat]=OFF    ;
 341:   ;
 342:  !Lamp USER 2 AAN op fanuc kast ;
 343:  DO[54:User2]=ON ;
 344:  WAIT DI[45:USER 2]=ON    ;
 345:   ;
 346:  !Lamp USER 2 AAN op fanuc kast ;
 347:  DO[54:User2]=OFF ;
 348:  END ;
 349:   ;
 350:  LBL[740:BUFFER VOL] ;
 351:  CALL TEKST(740) ;
 352:J P[1:rust positie] 30% FINE    ;
 353:  R[9:Act R8]=0    ;
 354:  R[43:Bufferstapel vol]=0    ;
 355:  CALL DUMPEN    ;
 356:  WAIT DI[7:Automaat]=OFF    ;
 357:   ;
 358:  !Lamp USER 2 AAN op fanuc kast ;
 359:  DO[54:User2]=ON ;
 360:  WAIT DI[45:USER 2]=ON    ;
 361:   ;
 362:  !Lamp USER 2 AAN op fanuc kast ;
 363:  DO[54:User2]=OFF ;
 364:   ;
 365:  LBL[741:Contrle buf leeg] ;
 366:  IF DI[22:Buffer vrij]=ON,JMP LBL[742] ;
 367:   ;
 368:  CALL TEKST(1200) ;
 369:  !Lamp USER 2 AAN op fanuc kast ;
 370:  DO[54:User2]=ON ;
 371:  WAIT DI[45:USER 2]=ON    ;
 372:   ;
 373:  !Lamp USER 2 AAN op fanuc kast ;
 374:  DO[54:User2]=OFF ;
 375:  JMP LBL[741] ;
 376:   ;
 377:  LBL[742:Buffer vrij] ;
 378:  !DO :RODE LAMP UIT ;
 379:  DO[49:Buzzer hek]=OFF ;
 380:  DO[50:Rode lamp hek]=OFF ;
 381:  END ;
 382:   ;
 383:  LBL[750:BUFFER NIET VRY] ;
 384:  CALL TEKST(1200) ;
 385:J P[1:rust positie] 30% FINE    ;
 386:  R[9:Act R8]=0    ;
 387:  R[39:Buffer niet vrij]=0    ;
 388:   ;
 389:  CALL DUMPEN    ;
 390:   ;
 391:  WAIT DI[7:Automaat]=OFF    ;
 392:   ;
 393:  !Lamp USER 2 AAN op fanuc kast ;
 394:  DO[54:User2]=ON ;
 395:  WAIT DI[45:USER 2]=ON    ;
 396:   ;
 397:  !Lamp USER 2 AAN op fanuc kast ;
 398:  DO[54:User2]=OFF ;
 399:   ;
 400:  LBL[751:Contrle buf leeg] ;
 401:  IF DI[22:Buffer vrij]=ON,JMP LBL[752] ;
 402:   ;
 403:  CALL TEKST(1200) ;
 404:  !Lamp USER 2 AAN op fanuc kast ;
 405:  DO[54:User2]=ON ;
 406:  WAIT DI[45:USER 2]=ON    ;
 407:   ;
 408:  !Lamp USER 2 AAN op fanuc kast ;
 409:  DO[54:User2]=OFF ;
 410:  JMP LBL[751] ;
 411:   ;
 412:  LBL[752:Buffer vry] ;
 413:  !DO :RODE LAMP UIT ;
 414:  DO[49:Buzzer hek]=OFF ;
 415:  DO[50:Rode lamp hek]=OFF ;
 416:  END ;
 417:   ;
 418:  LBL[760:Band vol] ;
 419:  CALL TEKST(760) ;
 420:J P[1:rust positie] 30% FINE    ;
 421:  Open hand 1    ;
 422:  Open hand 2    ;
 423:  R[44:Transportbnd vol]=0    ;
 424:   ;
 425:  WAIT DI[7:Automaat]=OFF    ;
 426:   ;
 427:  !Lamp USER 2 AAN op fanuc kast ;
 428:  DO[54:User2]=ON ;
 429:  WAIT DI[45:USER 2]=ON    ;
 430:   ;
 431:  !Lamp USER 2 AAN op fanuc kast ;
 432:  DO[54:User2]=OFF ;
 433:  END ;
 434:   ;
 435:  LBL[770:Grijpfout keren] ;
 436:  CALL TEKST(741) ;
 437:  R[83:Grijpcontr keren]=0    ;
 438:J P[1:rust positie] 30% FINE    ;
 439:  CALL DUMPEN    ;
 440:  WAIT DI[7:Automaat]=OFF    ;
 441:   ;
 442:  !Lamp USER 2 AAN op fanuc kast ;
 443:  DO[54:User2]=ON ;
 444:  WAIT DI[45:USER 2]=ON    ;
 445:   ;
 446:  !Lamp USER 2 AAN op fanuc kast ;
 447:  DO[54:User2]=OFF ;
 448:   ;
 449:  LBL[771:Cntle positi vry] ;
 450:  IF DI[71:Keerunit leeg]=OFF,JMP LBL[772] ;
 451:   ;
 452:  CALL TEKST(736) ;
 453:  !Lamp USER 2 AAN op fanuc kast ;
 454:  DO[54:User2]=ON ;
 455:  WAIT DI[45:USER 2]=ON    ;
 456:   ;
 457:  !Lamp USER 2 AAN op fanuc kast ;
 458:  DO[54:User2]=OFF ;
 459:  JMP LBL[771] ;
 460:   ;
 461:  LBL[772] ;
 462:  END ;
 463:   ;
 464:  LBL[775:Grijper niet ope] ;
 465:  CALL TEKST(737) ;
 466:  R[83:Grijpcontr keren]=0    ;
 467:  DO[41:Keerunit rust]=ON ;
 468:  DO[42:Keerunit actie]=OFF ;
 469:J P[1:rust positie] 30% FINE    ;
 470:  CALL DUMPEN    ;
 471:  WAIT DI[7:Automaat]=OFF    ;
 472:   ;
 473:  !Lamp USER 2 AAN op fanuc kast ;
 474:  DO[54:User2]=ON ;
 475:  WAIT DI[45:USER 2]=ON    ;
 476:   ;
 477:  !Lamp USER 2 AAN op fanuc kast ;
 478:  DO[54:User2]=OFF ;
 479:   ;
 480:  LBL[776:Cntle positi vry] ;
 481:  IF DI[71:Keerunit leeg]=OFF,JMP LBL[777] ;
 482:   ;
 483:  CALL TEKST(736) ;
 484:  !Lamp USER 2 AAN op fanuc kast ;
 485:  DO[54:User2]=ON ;
 486:  WAIT DI[45:USER 2]=ON    ;
 487:   ;
 488:  !Lamp USER 2 AAN op fanuc kast ;
 489:  DO[54:User2]=OFF ;
 490:  JMP LBL[776] ;
 491:   ;
 492:  LBL[777] ;
 493:  END ;
 494:   ;
 495:  LBL[778:Geen lucht aanw.] ;
 496:  CALL TEKST(778) ;
 497:  R[100:Product controle]=0    ;
 498:  R[84:Lucht keerunit]=0    ;
 499:J P[1:rust positie] 30% FINE    ;
 500:   ;
 501:  CALL DUMPEN    ;
 502:   ;
 503:  WAIT DI[7:Automaat]=OFF    ;
 504:   ;
 505:  !Lamp USER 2 AAN op fanuc kast ;
 506:  DO[54:User2]=ON ;
 507:  WAIT DI[45:USER 2]=ON    ;
 508:   ;
 509:  !Lamp USER 2 AAN op fanuc kast ;
 510:  DO[54:User2]=OFF ;
 511:  END ;
 512:   ;
 513:  !******HOME ZOEKEN****** ;
 514:  LBL[1000:HOME ZOEKEN] ;
 515:  CALL TEKST(1000) ;
 516:  PAYLOAD[1] ;
 517:  OVERRIDE=25% ;
 518:  PR[100:Home Lpos]=LPOS    ;
 519:  R[200:Homezoek X]=PR[100,1:Home Lpos]    ;
 520:  R[199:Homezoek Y]=PR[100,2:Home Lpos]    ;
 521:  R[198:Homezoek Z]=PR[100,3:Home Lpos]    ;
 522:   ;
 523:  PR[99:Home Jpos]=JPOS    ;
 524:  R[193:J4]=PR[99,4:Home Jpos]    ;
 525:  R[194:J6]=PR[99,6:Home Jpos]    ;
 526:   ;
 527:  R[189:Act Uframe]=$MNUFRAMENUM[1] ;
 528:  R[188:Act Utool]=$MNUTOOLNUM[1] ;
 529:   ;
 530:  PR[97:P6]=P[6:induik]    ;
 531:  R[192:P6-Y]=PR[97,2:P6]    ;
 532:  R[192:P6-Y]=R[192:P6-Y]+10    ;
 533:  PR[96:P11]=P[11:Na pak]    ;
 534:  R[191:P11-X]=PR[96,1:P11]    ;
 535:   ;
 536:  IF DO[100:Robot op reference]=ON,JMP LBL[1100] ;
 537:   ;
 538:  !--------------- ;
 539:  !--- ;
 540:  IF R[199:Homezoek Y]<500,JMP LBL[1010] ;
 541:  !--------------- ;
 542:   ;
 543:  IF R[199:Homezoek Y]<R[192:P6-Y],JMP LBL[1006] ;
 544:   ;
 545:  IF DI[1:Matrijs open]=ON,JMP LBL[1001] ;
 546:  CALL TEKST(1001) ;
 547:  WAIT DI[1:Matrijs open]=ON AND DI[3:Uitw. terug]=ON    ;
 548:   ;
 549:  LBL[1001] ;
 550:  Open hand 1    ;
 551:  Open hand 2    ;
 552:  PR[100,1:Home Lpos]=R[191:P11-X]    ;
 553:L PR[100:Home Lpos] 100mm/sec CNT20    ;
 554:   ;
 555:  LBL[1005] ;
 556:  UTOOL_NUM=1 ;
 557:L P[15:buiten SGM] 300mm/sec FINE    ;
 558:   ;
 559:  LBL[1006:Buiten SGM] ;
 560:L P[15:buiten SGM] 300mm/sec FINE    ;
 561:J P[3:naar SGM] 15% FINE    ;
 562:  JMP LBL[1100] ;
 563:   ;
 564:  !---------------- ;
 565:  !--- ;
 566:  LBL[1010] ;
 567:  CALL HOMEN    ;
 568:  JMP LBL[1100] ;
 569:  !--------------- ;
 570:   ;
 571:  LBL[1100] ;
 572:  R[90:Program gestart]=1    ;
 573:  UFRAME_NUM=1 ;
 574:  UTOOL_NUM=1 ;
 575:J P[1:rust positie] 30% CNT100    ;
 576:   ;
 577:  !Grijpers open ;
 578:  Open hand 1    ;
 579:  Open hand 2    ;
 580:   ;
 581:  R[7:Steekhoogte]=13.2    ;
 582:  R[9:Act R8]=0    ;
 583:  R[6:Aantal in hoogte]=23    ;
 584:  R[21:Grijper 1-voudig]=0    ;
 585:  R[35:Vertrag. induik]=0    ;
 586:  R[36:Grijpfout masker]=0    ;
 587:  R[38:Hoogte fout]=0    ;
 588:  R[39:Buffer niet vrij]=0    ;
 589:  R[40:Grijpfout centre]=0    ;
 590:  R[41:Geen vrijg  slot]=0    ;
 591:  R[42:Grijpfout bufffe]=0    ;
 592:  R[43:Bufferstapel vol]=0    ;
 593:  R[45:Teller ontstapel]=0    ;
 594:  R[46:Cyclusteller]=0    ;
 595:  R[47:Centrer niet vri]=0    ;
 596:  R[49:Snelheid]=100    ;
 597:  R[81:Kerunit niet vry]=0    ;
 598:  R[82:Grijper open]=0    ;
 599:  R[83:Grijpcontr keren]=0    ;
 600:  R[84:Lucht keerunit]=0    ;
 601:  R[94:Wait forever]=0    ;
 602:   ;
 603:  !PRINTER AFSTELING ;
 604:  !Waardes ophalen uit positie ;
 605:  PR[88:Offs print 1A]=P[201]    ;
 606:  PR[89:Offs print 2A]=P[202]    ;
 607:   ;
 608:  DO[51:IONblower Corona]=OFF ;
 609:  DO[52:IONblower witband]=ON ;
 610:  DO[49:Buzzer hek]=OFF ;
 611:  DO[50:Rode lamp hek]=OFF ;
 612:   ;
 613:  !WITTE TRANSPORTBAND AAN ;
 614:  DO[14321]=PULSE,6.0sec ;
 615:   ;
 616:  !Controle centreer leeg ;
 617:  LBL[2000] ;
 618:  IF DI[23:Positioneer vrij]=ON,JMP LBL[2100] ;
 619:   ;
 620:  !Controle buffer leeg ;
 621:  LBL[2001] ;
 622:  IF DI[22:Buffer vrij]=OFF,JMP LBL[2101] ;
 623:   ;
 624:  !Controle lucht aanwezig op ;
 625:  !Tussencentreer ;
 626:  LBL[2002] ;
 627:  IF DI[16:Magazijnklep dicht]=OFF,JMP LBL[2102] ;
 628:   ;
 629:  !AANSTURING SNAARBANDJE--- JA ;
 630:  !AANSTURING CORONA-------- NEE ;
 631:  !AANSTURING ANTI STATIC--- JA ;
 632:  !AANSTURING WHITE STRIPE-- NEE ;
 633:  DO[15020]=OFF ;
 634:  DO[15021]=ON ;
 635:  DO[15022]=OFF ;
 636:  DO[15023]=OFF ;
 637:  DO[15024]=OFF ;
 638:  DO[15025]=OFF ;
 639:  DO[15026]=OFF ;
 640:   ;
 641:  !Reset tafel vol ;
 642:  DO[14419]=OFF ;
 643:  END ;
 644:   ;
 645:  LBL[2100:Opstart controle] ;
 646:  CALL TEKST(1300) ;
 647:  !Lamp USER 2 AAN op fanuc kast ;
 648:  DO[54:User2]=ON ;
 649:  WAIT DI[45:USER 2]=ON    ;
 650:   ;
 651:  !Lamp USER 2 AAN op fanuc kast ;
 652:  DO[54:User2]=OFF ;
 653:  JMP LBL[2000] ;
 654:   ;
 655:  LBL[2101:Opstart controle] ;
 656:  CALL TEKST(1200) ;
 657:  !Lamp USER 2 AAN op fanuc kast ;
 658:  DO[54:User2]=ON ;
 659:  WAIT DI[45:USER 2]=ON    ;
 660:   ;
 661:  !Lamp USER 2 AAN op fanuc kast ;
 662:  DO[54:User2]=OFF ;
 663:  JMP LBL[2001] ;
 664:   ;
 665:  LBL[2102:controle lucht] ;
 666:  CALL TEKST(778) ;
 667:  !Lamp USER 2 AAN op fanuc kast ;
 668:  DO[54:User2]=ON ;
 669:  WAIT DI[45:USER 2]=ON    ;
 670:   ;
 671:  !Lamp USER 2 AAN op fanuc kast ;
 672:  DO[54:User2]=OFF ;
 673:  JMP LBL[2002] ;
 674:   ;
 675:  END ;
 676:  !-------------------------------- ;
 677:  !Offset registers voor printer ;
 678:  !Offset waarden opslaan ;
 679:J P[201] 1% FINE    ;
 680:J P[202] 1% FINE    ;
 681:  !-------------------------------- ;
/POS
P[1:"rust positie"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   451.193  mm,	Y =      .016  mm,	Z =   354.128  mm,
	W =   179.901 deg,	P =      .000 deg,	R =   -90.100 deg
};
P[3:"naar SGM"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   279.404  mm,	Y =   418.185  mm,	Z =   527.595  mm,
	W =   179.916 deg,	P =   -27.516 deg,	R =     1.768 deg
};
P[6:"induik"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   285.000  mm,	Y =   550.000  mm,	Z =   610.350  mm,
	W =   -83.262 deg,	P =   -89.285 deg,	R =   -96.679 deg
};
P[9:"voor pak"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   285.000  mm,	Y =  1406.500  mm,	Z =   610.350  mm,
	W =   -83.263 deg,	P =   -89.284 deg,	R =   -96.679 deg
};
P[10:"pak pos"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   324.850  mm,	Y =  1406.494  mm,	Z =   610.350  mm,
	W =   -83.261 deg,	P =   -89.284 deg,	R =   -96.681 deg
};
P[11:"Na pak"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   275.000  mm,	Y =  1406.500  mm,	Z =   610.350  mm,
	W =   -83.258 deg,	P =   -89.284 deg,	R =   -96.684 deg
};
P[15:"buiten SGM"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   275.000  mm,	Y =   550.000  mm,	Z =   610.350  mm,
	W =   -83.258 deg,	P =   -89.284 deg,	R =   -96.684 deg
};
P[50:"deurpos"]{
   GP1:
	UF : 1, UT : 1,		CONFIG : 'N U T, 0, 0, 0',
	X =   436.228  mm,	Y =      .013  mm,	Z =   420.765  mm,
	W =   179.900 deg,	P =     -.000 deg,	R =   -90.100 deg
};
P[201]{
   GP1:
	UF : 0, UT : 1,		CONFIG : 'F U T, 0, 0, 0',
	X =     0.000  mm,	Y =     0.000  mm,	Z =     0.000  mm,
	W =     0.000 deg,	P =     0.000 deg,	R =     1.000 deg
};
P[202]{
   GP1:
	UF : 0, UT : 1,		CONFIG : 'F U T, 0, 0, 0',
	X =     0.000  mm,	Y =     0.000  mm,	Z =     0.000  mm,
	W =     0.000 deg,	P =     0.000 deg,	R =     1.500 deg
};
/END
